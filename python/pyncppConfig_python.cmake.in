# Copyright (c) 2024 IHU Liryc, Universit√© de Bordeaux, Inria.
# License: BSD-3-Clause

if(NOT pyncpp_FIND_REQUIRED_COMPONENTS OR python IN_LIST pyncpp_FIND_COMPONENTS)

    set(pyncpp_python_FOUND TRUE)
    set(pyncpp_PYTHON_VERSION_MAJOR @PYNCPP_PYTHON_VERSION_MAJOR@)
    set(pyncpp_PYTHON_VERSION_MINOR @PYNCPP_PYTHON_VERSION_MINOR@)
    set(pyncpp_PYTHON_VERSION_PATCH @PYNCPP_PYTHON_VERSION_PATCH@)
    set(pyncpp_PYTHON_SHORT_VERSION @PYNCPP_PYTHON_SHORT_VERSION@)
    set(pyncpp_PYTHON_VERSION @PYNCPP_PYTHON_VERSION@)

    set(pyncpp_PYTHON_INSTALL_SUBDIR "@PYNCPP_PYTHON_SUBDIR@")
    set(pyncpp_PYTHON_DIR "@PYNCPP_PYTHON_DIR@")

    file(RELATIVE_PATH pyncpp_RELATIVE_PYTHON_DIR "${pyncpp_ROOT}/${PYNCPP_RUNTIME_SUBDIR}" "${pyncpp_PYTHON_DIR}")

    if(WIN32)
        set(pyncpp_PYTHON_SITE_DIR "${pyncpp_PYTHON_DIR}/lib/site-packages")
    else()
        set(pyncpp_PYTHON_SITE_DIR "${pyncpp_PYTHON_DIR}/lib/python${pyncpp_PYTHON_SHORT_VERSION}/site-packages")
    endif()

    if(NOT TARGET pyncpp_python_library)
        if(WIN32)
            set(_version ${pyncpp_PYTHON_VERSION_MAJOR}${pyncpp_PYTHON_VERSION_MINOR})
        else()
            set(_version ${pyncpp_PYTHON_SHORT_VERSION})
        endif()

        set(_include_dir)

        if(WIN32)
            if(@CONFIG@ STREQUAL "DEBUG")
                set(_debug_suffix "_d")
            else()
                set(_debug_suffix "")
            endif()

            set(_lib_path "python${_version}${_debug_suffix}.dll")
            set(_devlib_path "libs/python${_version}${_debug_suffix}.lib")
        else()
            set(_lib_path "lib/libpython${_version}${CMAKE_SHARED_LIBRARY_SUFFIX}")
        endif()

        if(EXISTS "${pyncpp_PYTHON_DIR}/${_lib_path}")
            add_library(pyncpp_python_library SHARED IMPORTED GLOBAL)
            set(pyncpp_PYTHON_LIBRARY pyncpp_python_library)

            set_property(TARGET pyncpp_python_library APPEND PROPERTY IMPORTED_CONFIGURATIONS @CONFIG@)

            set_target_properties(pyncpp_python_library PROPERTIES
                IMPORTED_LOCATION "${pyncpp_PYTHON_DIR}/${_lib_path}"
                IMPORTED_LOCATION_@CONFIG@ "${pyncpp_PYTHON_DIR}/${_lib_path}"
                )

            if(WIN32)
                set_target_properties(pyncpp_python_library PROPERTIES
                    IMPORTED_IMPLIB "${pyncpp_PYTHON_DIR}/${_devlib_path}"
                    IMPORTED_IMPLIB_@CONFIG@ "${pyncpp_PYTHON_DIR}/${_devlib_path}"
                    )
                set(_include_dir "${pyncpp_PYTHON_DIR}/include")
            else()
                set(_include_dir "${pyncpp_PYTHON_DIR}/include/python@PYNCPP_PYTHON_SHORT_VERSION@")
            endif()

            target_include_directories(pyncpp_python_library INTERFACE "${_include_dir}")
        else()
            set(pyncpp_FOUND FALSE)
        endif()
    endif()

endif()
